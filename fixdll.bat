@echo off
rem
rem  Uses SED to generate the module definition file for linking into
rem  a DLL, and the rsxdll.h header that tricks DLL variable references
rem  into working with RSXNT.
rem

echo Scanning for global symbols...
gcc -E -I. -I./include -DSCAN_EXPORT -DALLEGRO_API -o _apidef.tmp src/win/dllexp.c
sed -n -e "s/^ *alldll[fi][un][nl]  *\**\(.*\)_dll.*/    \1/p" _apidef.tmp > _apidef1.tmp
sed -n -e "s/^ *alldll[vfa][apr][rtr]  *\**\(.*\)_dll.*/    \1 DATA/p" _apidef.tmp >> _apidef1.tmp
sort _apidef1.tmp > _apidef2.tmp

gcc -E -I. -I./include -DSCAN_EXPORT -DALLEGRO_WINAPI -o _wapidef.tmp src/win/dllexp.c
sed -n -e "s/^ *alldll[fi][un][nl]  *\**\(.*\)_dll.*/    \1/p" _wapidef.tmp > _wapidef1.tmp
sed -n -e "s/^ *alldll[vfa][apr][rtr]  *\**\(.*\)_dll.*/    \1 DATA/p" _wapidef.tmp >> _wapidef1.tmp
sort _wapidef1.tmp > _wapidef2.tmp

gcc -E -I. -I./include -DSCAN_EXPORT -DALLEGRO_INTERNALS -o _intdef.tmp src/win/dllexp.c
sed -n -e "s/^ *alldll[fi][un][nl]  *\**\(.*\)_dll.*/    \1/p" _intdef.tmp > _intdef1.tmp
sed -n -e "s/^ *alldll[vfa][apr][rtr]  *\**\(.*\)_dll.*/    \1 DATA/p" _intdef.tmp >> _intdef1.tmp
sort _intdef1.tmp > _intdef2.tmp

copy _apidef2.tmp + _wapidef2.tmp + _intdef2.tmp _alldef2.tmp > nul

echo ; generated by fixdll.bat > _all.def
echo EXPORTS >> _all.def
sed -e "p" -e "=" -e "d" _alldef2.tmp > _alldef3.tmp
sed -e "N" -e "s/\n/ @/" -e "s/DATA \(.*\)/\1 DATA/" _alldef3.tmp >> _all.def

echo Generating lib\msvc\allegro.def...
copy _all.def lib\msvc\allegro.def > nul

echo Generating lib\mingw32\allegro.def...
copy _all.def lib\mingw32\allegro.def > nul

echo Generating include\allegro\rsxdll.h...
echo /* generated by fixdll.bat for RSXNT */ > include\allegro\rsxdll.h
sed -n -e "s/^alldll[vf][ap][rt] *\**\(.*\)_dll.*/#define \1 (*\1)/p" _apidef.tmp >> include\allegro\rsxdll.h
sed -n -e "s/^alldll[vf][ap][rt] *\**\(.*\)_dll.*/#define \1 (*\1)/p" _wapidef.tmp >> include\allegro\rsxdll.h
sed -n -e "s/^alldll[vf][ap][rt] *\**\(.*\)_dll.*/#define \1 (*\1)/p" _intdef.tmp >> include\allegro\rsxdll.h

del _*def*.tmp
del _all.def

echo Done!
